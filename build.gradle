// Copyright (c) 2021 Synopsys, Inc. All rights reserved worldwide.
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'java-library'
    id 'org.jenkins-ci.jpi' version '0.43.0'
}

group = 'io.jenkins.plugins'
version = "2022.1.0"
description = 'Allows users to run Synopsys Rapid Scan Static commands in their builds'

jenkinsPlugin {
    jenkinsVersion = '2.263.1'
    displayName = 'Synopsys Rapid Scan Static Plugin'
    shortName = "synopsys-sigma"
    url = 'https://github.com/jenkinsci/synopsys-sigma-plugin'

    pluginFirstClassLoader = false

    licenses {
        license {
            name = 'MIT License'
            distribution = 'repo'
            url = 'https://opensource.org/licenses/MIT'
        }
    }
}

dependencies {
    implementation 'org.jenkins-ci.plugins:credentials:2.1.10'
    implementation 'org.jenkins-ci.plugins:matrix-project:1.18'
    implementation 'io.jenkins.plugins:warnings-ng:9.4.0'

    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.22'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-cps:2.93'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-durable-task-step:1.37'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-job:2.41'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.18.3'
}

task renameFile {
    doLast {
        if (project.version.toString().contains("SNAPSHOT")) {
            File libsDirectory = new File(project.buildDir, "libs")
            File originalFile = new File(libsDirectory, "${project.getName()}.hpi")
            File newFile = new File(libsDirectory, "${project.getName()}-${project.version}.hpi")
            originalFile.renameTo newFile
        }
    }
}

jpi.finalizedBy renameFile
